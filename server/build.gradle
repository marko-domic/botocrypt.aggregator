plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'idea'
}

group = 'com.botocrypt'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    compileOnly.extendsFrom annotationProcessor
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimOnly.extendsFrom runtimeOnly
    integrationTestCompileOnly.extendsFrom compileOnly
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor
}

dependencies {

    implementation project(':arbitrage_api')
    implementation project(':cex_api')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.quartz-scheduler:quartz:2.3.2'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation "org.springframework:spring-context-support:${spring_version}"
    implementation 'com.h2database:h2:1.4.200'
    implementation "org.springframework:spring-messaging:${spring_version}"
    implementation 'commons-io:commons-io:2.11.0'
    implementation "org.springframework:spring-webmvc:${spring_version}"
    implementation 'org.flywaydb:flyway-core:8.5.4'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.h2database:h2:1.4.200'
    testImplementation 'org.mockito:mockito-inline:4.3.1'

    implementation 'mysql:mysql-connector-java:8.0.28'

    implementation "io.grpc:grpc-stub:${grpc_version}"
    implementation "io.grpc:grpc-protobuf:${grpc_version}"
    implementation "io.grpc:grpc-services:${grpc_version}"

    integrationTestImplementation "org.springframework.boot:spring-boot-starter-test:${spring_boot_version}"
    integrationTestImplementation 'com.github.tomakehurst:wiremock:2.27.2'
    integrationTestImplementation 'org.awaitility:awaitility:4.2.0'
    integrationTestImplementation 'org.grpcmock:grpcmock-spring-boot:0.7.5'
    integrationTestImplementation "io.grpc:grpc-testing:${grpc_version}"
    integrationTestImplementation "io.grpc:grpc-testing-proto:${grpc_version}"
    integrationTestImplementation 'org.mockito:mockito-inline:4.3.1'
    integrationTestImplementation 'org.testcontainers:mysql:1.16.3'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    integrationTestImplementation 'com.h2database:h2:1.4.200'
    integrationTestCompileOnly 'org.projectlombok:lombok'
    integrationTestAnnotationProcessor 'org.projectlombok:lombok'

}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = 'Runs integration tests'
    group = 'verification'

    systemProperty 'spring.profiles.include', 'test'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform {
        includeTags 'integration'
    }
    shouldRunAfter test
}

check.dependsOn integrationTest

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}
